cmake_minimum_required(VERSION 2.6 FATAL_ERROR)

project(minisat)
set(CMAKE_CXX_STANDARD 11)

set(CMAKE_CXX_STANDARD_REQUIRED ON)
if (CMAKE_VERSION VERSION_LESS "3.1")
      set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=gnu++11")
else ()
    set (CMAKE_CXX_STANDARD 11)
endif ()

#--------------------------------------------------------------------------------------------------
# Configurable options:

option(STATIC_BINARIES "Link binaries statically." ON)
option(USE_SORELEASE   "Use SORELEASE in shared library filename." ON)

#--------------------------------------------------------------------------------------------------
# Library version:

set(MINISAT_SOMAJOR   2)
set(MINISAT_SOMINOR   1)
set(MINISAT_SORELEASE 0)

# Compute VERSION and SOVERSION:
if (USE_SORELEASE)
  set(MINISAT_VERSION ${MINISAT_SOMAJOR}.${MINISAT_SOMINOR}.${MINISAT_SORELEASE})
else()
  set(MINISAT_VERSION ${MINISAT_SOMAJOR}.${MINISAT_SOMINOR})
endif()
set(MINISAT_SOVERSION ${MINISAT_SOMAJOR})

#--------------------------------------------------------------------------------------------------
# Dependencies:

find_package(ZLIB)
include_directories(${ZLIB_INCLUDE_DIR})
include_directories(${minisat_SOURCE_DIR})

#--------------------------------------------------------------------------------------------------
# Compile flags:

add_definitions(-D__STDC_FORMAT_MACROS -D__STDC_LIMIT_MACROS)

#--------------------------------------------------------------------------------------------------
# Build Targets:

set(MINISAT_LIB_SOURCES
    minisat/utils/Options.cc
    minisat/utils/System.cc
    minisat/core/Solver.cc
    minisat/simp/SimpSolver.cc)

set(MINIMAL_LIB_SOURCES
        minimal/MMSolver.cpp
        minimal/MMSolver.h
        minimal/MRSolver.cpp
        minimal/MRSolver.h
        minimal/Graph.cpp
        minimal/Graph.h
        minimal/Utils.h
        minimal/BaseSolver.cpp
        minimal/BaseSolver.h
        minimal/SpaceDefine.h)

add_library(minisat-lib-static STATIC ${MINISAT_LIB_SOURCES})
add_library(minisat-lib-shared SHARED ${MINISAT_LIB_SOURCES})

add_library(minimal-lib-static STATIC ${MINIMAL_LIB_SOURCES})
add_library(minimal-lib-shared SHARED ${MINIMAL_LIB_SOURCES})

target_link_libraries(minisat-lib-shared ${ZLIB_LIBRARY})
target_link_libraries(minisat-lib-static ${ZLIB_LIBRARY})

target_link_libraries(minimal-lib-shared minisat-lib-shared ${ZLIB_LIBRARY})
target_link_libraries(minimal-lib-static minisat-lib-static ${ZLIB_LIBRARY})

add_executable(minisat_core minisat/core/Main.cc)
add_executable(minisat_simp minisat/simp/Main.cc)
add_executable(minimal minimal/Main.cpp )
add_executable(minimal_diagnosis  diagnosis/Main.cpp diagnosis/DiagnosisSATSolver.cpp diagnosis/DiagnosisBaseSolver.cpp diagnosis/DiagnosisBaseSolver.h diagnosis/DiagnosisMinimalSolver.cpp diagnosis/DiagnosisMinimalSolver.h)

if(STATIC_BINARIES)
    target_link_libraries(minisat_core minisat-lib-static)
    target_link_libraries(minisat_simp minisat-lib-static)
    target_link_libraries(minimal minimal-lib-static)
    target_link_libraries(minimal_diagnosis minimal-lib-static)
else()
    target_link_libraries(minisat_core minisat-lib-shared)
    target_link_libraries(minisat_simp minisat-lib-shared)
    target_link_libraries(minimal minimal-lib-shared)
    target_link_libraries(minimal_diagnosis minimal-lib-shared)
endif()

set_target_properties(minisat-lib-static PROPERTIES OUTPUT_NAME "minisat")
set_target_properties(minisat-lib-shared
  PROPERTIES
    OUTPUT_NAME "minisat" 
    VERSION ${MINISAT_VERSION}
    SOVERSION ${MINISAT_SOVERSION})

set_target_properties(minisat_simp       PROPERTIES OUTPUT_NAME "minisat")

#--------------------------------------------------------------------------------------------------
# Installation targets:

install(TARGETS minisat-lib-static minisat-lib-shared minisat_core minisat_simp 
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION lib
        ARCHIVE DESTINATION lib)

install(DIRECTORY minisat/mtl minisat/utils minisat/core minisat/simp
        DESTINATION include/minisat
        FILES_MATCHING PATTERN "*.h")
